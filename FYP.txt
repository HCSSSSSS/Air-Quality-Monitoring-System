											Arduino Uno R3
// Include libraries for serial communication and MH-Z19 CO₂ sensor
#include <SoftwareSerial.h> 
#include <MHZ19.h>

// Define analog input pins for gas sensors
#define MQ2_PIN A0       // MQ-2 sensor for combustible gas detection
#define CO_PIN A1        // MICS-6814 sensor channel for Carbon Monoxide (CO)
#define NO2_PIN A2       // MICS-6814 sensor channel for Nitrogen Dioxide (NO₂)
#define NH3_PIN A3       // MICS-6814 sensor channel for Ammonia (NH₃)

// Define pins for SoftwareSerial communication
#define RX_PIN 10        // Arduino receive pin for MH-Z19 sensor
#define TX_PIN 11        // Arduino transmit pin for MH-Z19 sensor
#define ESP_TX 3         // Arduino transmit pin to ESP32
#define ESP_RX 2         // Arduino receive pin from ESP32

// Create SoftwareSerial objects
SoftwareSerial mySerial(RX_PIN, TX_PIN);     // Serial for MH-Z19 CO₂ sensor
SoftwareSerial espSerial(ESP_RX, ESP_TX);    // Serial for communication with ESP32

// Create MH-Z19 sensor object
MHZ19 myMHZ19;

void setup() {
    // Initialize hardware serial for debugging
    Serial.begin(115200);

    // Initialize software serial for ESP32 communication
    espSerial.begin(115200);

    // Initialize software serial for MH-Z19 sensor communication
    mySerial.begin(9600);
    myMHZ19.begin(mySerial);

    // Display initialization message
    Serial.println("Arduino Initializing...");
    delay(3000); // Wait for devices to stabilize
}

void loop() {
    // Read analog values from gas sensors
    int mq2Value = analogRead(MQ2_PIN);
    int coValue = analogRead(CO_PIN);
    int no2Value = analogRead(NO2_PIN);
    int nh3Value = analogRead(NH3_PIN);

    // Read CO₂ concentration in ppm from MH-Z19 sensor
    int co2ppm = myMHZ19.getCO2();

    // Send sensor data to ESP32 via SoftwareSerial
    espSerial.print(mq2Value);
    espSerial.print(",");
    espSerial.print(coValue);
    espSerial.print(",");
    espSerial.print(no2Value);
    espSerial.print(",");
    espSerial.print(nh3Value);
    espSerial.print(",");
    espSerial.println(co2ppm);

    // Output sensor readings to Serial Monitor for debugging
    Serial.println("====================================");
    Serial.print("Combustible gas (MQ-2): ");
    Serial.println(mq2Value);
    
    Serial.print("CO (MICS-6814): ");
    Serial.println(coValue);

    Serial.print("NO₂ (MICS-6814): ");
    Serial.println(no2Value);

    Serial.print("NH₃ (MICS-6814): ");
    Serial.println(nh3Value);

    Serial.print("CO₂ (MH-Z19B): ");
    Serial.print(co2ppm);
    Serial.println(" ppm");

    Serial.println("====================================");

    // Wait 10 seconds before next reading
    delay(10000);
}

										ESP32
#include <Arduino.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <SdsDustSensor.h>
#include <SPI.h>
#include <SD.h>
#include <time.h> // For NTP time synchronization

// ========== 1) WiFi Configuration ==========
const char* ssid = "HCSS";          // Your WiFi Name
const char* password = "123456789"; // Your WiFi password
const char* serverUrl = "http://192.168.137.1:3000/upload"; // Your server address

// ========== 2) NTP  ==========
const char* ntpServer = "pool.ntp.org"; 
const long gmtOffset_sec = 8 * 3600;      
const int daylightOffset_sec = 0;        

// ========== 3) OLED  ==========
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET   -1
#define SCREEN_ADDRESS 0x3C

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

void setupDisplay() {
  if (!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS)) {
    Serial.println(F("OLED Display initialization failed!"));
    for (;;);
  }
  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.display();
}

// ========== 4) Serial port definition ==========
#define ARDUINO_RX_PIN 4   // Arduino TX -> ESP32 GPIO4
#define ARDUINO_TX_PIN 17  // Arduino RX -> ESP32 GPIO17
#define SDS011_RX_PIN 25   // SDS011 TX -> ESP32 GPIO25
#define SDS011_TX_PIN 26   // SDS011 RX -> ESP32 GPIO26

// ========== 5) Global variables ==========
int arduinoValues[5] = {0, 0, 0, 0, 0}; // Raw data：MQ-2, CO, NO2, NH3, CO2
float pm25 = 0.0, pm10 = 0.0;           // SDS011 data

// Add page control variables: 0 means display Arduino data (adjusted), 1 means display SDS011 data
int currentPage = 0;

// ========== 6) Timer ==========
unsigned long previousDataTime = 0;
const unsigned long dataInterval = 10000; // Refresh data every 10 seconds
unsigned long previousPageTime = 0;
const unsigned long pageInterval = 5000;  // Switch display pages every 5 seconds

// ========== 7) OLED display function ==========
// Display Arduino data (adjusted data, consistent with the front end)：
void displayArduinoData() {
  float coAdjusted  = arduinoValues[1] * 0.1; 
  float no2Adjusted = arduinoValues[2] * 0.1; 
  float co2Adjusted = arduinoValues[4] / 2.0; 

  display.clearDisplay();

  // Combustible Gas (MQ-2)
  display.setCursor(0, 0);
  display.print("Gas: ");
  display.print(arduinoValues[0]);
  display.print(" ppm");

  // CO
  display.setCursor(0, 8);
  display.print("CO: ");
  display.print(coAdjusted, 1);
  display.print(" ppm");

  // NO2
  display.setCursor(0, 16);
  display.print("NO2: ");
  display.print(no2Adjusted, 1);
  display.print(" ppb");

  // NH3 
  display.setCursor(0, 24);
  display.print("NH3: ");
  display.print(arduinoValues[3]);
  display.print(" ppb");

  // CO2
  display.setCursor(0, 32);
  display.print("CO2: ");
  display.print(co2Adjusted, 1);
  display.print(" ppm");

  display.display();
}

//  SDS011 
void displaySDS011Data() {
  display.clearDisplay();

  display.setCursor(0, 0);
  display.print("PM2.5: ");
  display.print(pm25 + 10, 1);
  display.print(" ug/m3");

  display.setCursor(0, 8);
  display.print("PM10: ");
  display.print(pm10 + 10, 1);
  display.print(" ug/m3");

  display.display();
}

// Switch display content according to currentPage
void updateDisplay() {
  if (currentPage == 0) {
    displayArduinoData();
  } else {
    displaySDS011Data();
  }
}

// ========== 8) SDS011 Sensor Object ==========
HardwareSerial sdsSerial(1);
SdsDustSensor sds011(sdsSerial);

// ========== 9) WiFi data upload function ==========
void sendToServer() {
  // Construct JSON data and use the same adjustments as the frontend
  String jsonData = "{";
  jsonData += "\"mq2\":"  + String(arduinoValues[0]) + ",";
  jsonData += "\"co\":"   + String(arduinoValues[1] * 0.1) + ",";
  jsonData += "\"no2\":"  + String(arduinoValues[2] * 0.1) + ",";
  jsonData += "\"nh3\":"  + String(arduinoValues[3]) + ",";
  jsonData += "\"co2\":"  + String(arduinoValues[4] / 2.0) + ",";
  jsonData += "\"pm25\":" + String(pm25 + 10 , 1) + ",";
  jsonData += "\"pm10\":" + String(pm10 + 10 , 1);
  jsonData += "}";

  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    http.begin(serverUrl);
    http.addHeader("Content-Type", "application/json");

    Serial.print("Sending JSON: ");
    Serial.println(jsonData);

    int httpResponseCode = http.POST(jsonData);
    Serial.print("HTTP Response Code: ");
    Serial.println(httpResponseCode);

    http.end();
  } else {
    Serial.println("WiFi not connected!");
  }
}

// ========== 10) SD Card Configuration ==========
#define SD_CS_PIN 5
bool sdInitialized = false;

// ========== 11) Buzzer Configuration ==========
#define BUZZER_PIN 16  

// Set the alarm threshold (unit based on raw data)
const int THRESHOLD_MQ2    = 600;
const int THRESHOLD_CO     = 1000;
const int THRESHOLD_NO2    = 1500;
const int THRESHOLD_NH3    = 1200;
const int THRESHOLD_CO2    = 2000;
const float THRESHOLD_PM25 = 25.0;
const float THRESHOLD_PM10 = 50.0;

bool needAlarm = false;

// ========== 12)NTP time synchronization ==========
void setupTime() {
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
  Serial.print("Syncing time");
  while (true) {
    time_t now = time(nullptr);
    if (now > 8 * 3600) { // Determine whether the synchronization is successful
      Serial.println("\nTime sync OK!");
      break;
    }
    Serial.print(".");
    delay(1000);
  }
}

// ========== 13)SD card write function ==========
void writeDataToSD() {
  if (!sdInitialized) return;

  File file = SD.open("/sensor_data.csv", FILE_APPEND);
  if (!file) {
    Serial.println("Failed to open sensor_data.csv for writing!");
    return;
  }

  if (file.size() == 0) {
    file.println("MQ2,CO,NO2,NH3,CO2,PM2.5,PM10,Time");
  }

  time_t now = time(nullptr);
  struct tm* timeinfo = localtime(&now);
  char timeString[25];
  strftime(timeString, sizeof(timeString), "%F %T", timeinfo);

  file.print(arduinoValues[0]); file.print(",");
  file.print(arduinoValues[1] * 0.1); file.print(",");
  file.print(arduinoValues[2] * 0.1); file.print(",");
  file.print(arduinoValues[3]); file.print(",");
  file.print(arduinoValues[4] / 2.0); file.print(",");
  file.print(pm25 + 10, 1);          file.print(",");
  file.print(pm10 + 10, 1);          file.print(",");
  file.println(timeString);

  file.close();
  Serial.println("Data saved to sensor_data.csv");
}

// ========== 14) setup() function ==========
void setup() {
  Serial.begin(115200);
  Serial.println("===== ESP32: Starting =====");

  // Initialize the OLED display
  setupDisplay();

  // Connect to WiFi
  Serial.print("Connecting WiFi: ");
  Serial.println(ssid);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi Connected!");
  Serial.print("IP: ");
  Serial.println(WiFi.localIP());

  // Synchronize NTP time
  setupTime();

 // Initialize the serial port
  Serial2.begin(115200, SERIAL_8N1, ARDUINO_RX_PIN, ARDUINO_TX_PIN);
  Serial.println("Serial2 (Arduino) ready.");
  sdsSerial.begin(9600, SERIAL_8N1, SDS011_RX_PIN, SDS011_TX_PIN);
  Serial.println("SDS011 ready.");

  //SDS011 active mode
  sds011.setActiveReportingMode();

  previousDataTime = millis();
  previousPageTime = millis();

  // Initialize the buzzer pin
  pinMode(BUZZER_PIN, OUTPUT);

  // Initialize SD card
  Serial.println("Init SD...");
  if (!SD.begin(SD_CS_PIN)) {
    Serial.println("SD failed, no file logging!");
    sdInitialized = false;
  } else {
    Serial.println("SD OK.");
    sdInitialized = true;
  }

  // Initial display
  updateDisplay();
}

// ========== 15) loop() function ==========
void loop() {
  unsigned long currentMillis = millis();

  // Refresh data every 10 seconds
  if (currentMillis - previousDataTime >= dataInterval) {
    previousDataTime = currentMillis;

    // 1) Read SDS011 data (PM2.5 and PM10)
    PmResult pm = sds011.queryPm();
    if (pm.isOk()) {
      pm25 = pm.pm25;
      pm10 = pm.pm10;
      Serial.printf("SDS011: PM2.5=%.1f, PM10=%.1f\n", pm25, pm10);
    } else {
      Serial.println("SDS011 read failed.");
    }

    // 2) Read Arduino data (from Serial2)
    if (Serial2.available()) {
      String data = Serial2.readStringUntil('\n');
      Serial.println("Got Arduino data: " + data);
      int values[5];
      int index = 0;
      char *ptr = strtok((char*)data.c_str(), ",");
      while (ptr != NULL && index < 5) {
        values[index++] = atoi(ptr);
        ptr = strtok(NULL, ",");
      }
      if (index == 5) {
        for (int i = 0; i < 5; i++) {
          arduinoValues[i] = values[i];
        }
        Serial.printf("Arduino: MQ2=%d, CO=%d, NO2=%d, NH3=%d, CO2=%d\n",
                      arduinoValues[0], arduinoValues[1],
                      arduinoValues[2], arduinoValues[3],
                      arduinoValues[4]);
      } else {
        Serial.println("Arduino data parse error!");
      }
    } else {
      Serial.println("No Arduino data.");
    }

    // 3) Update OLED display (display corresponding data according to the current page)
    updateDisplay();

    // Page switching: switch the display page every 5 seconds
    if (currentMillis - previousPageTime >= pageInterval) {
      previousPageTime = currentMillis;
      currentPage = (currentPage + 1) % 2;
      updateDisplay();
    }

    // 4) Upload data to the server
    sendToServer();

    // 5) Write to SD card (append timestamp)
    writeDataToSD();

    // 6) Determine whether the threshold is exceeded and trigger the buzzer (the alarm judgment is based on the original data)
    needAlarm = false;
    if (arduinoValues[0] > THRESHOLD_MQ2) needAlarm = true; // MQ2
    if (arduinoValues[1] > THRESHOLD_CO)  needAlarm = true; // CO
    if (arduinoValues[2] > THRESHOLD_NO2) needAlarm = true; // NO2
    if (arduinoValues[3] > THRESHOLD_NH3) needAlarm = true; // NH3
    if (arduinoValues[4] > THRESHOLD_CO2) needAlarm = true; // CO2
    if (pm25 > THRESHOLD_PM25)            needAlarm = true; // PM2.5
    if (pm10 > THRESHOLD_PM10)            needAlarm = true; // PM10

    if (needAlarm) {
      Serial.println("Sensor values exceed thresholds, triggering alarm beep!");
      tone(BUZZER_PIN, 4000, 300);
    } else {
      noTone(BUZZER_PIN);
      Serial.println("Values normal, no alarm.");
    }
  }

  delay(100);
}

												Server.js 
// server.js
const express = require('express');
const bodyParser = require('body-parser');
const cors = require('cors');

const app = express();
// Parsing JSON request body
app.use(bodyParser.json());
// Allow cross-domain
app.use(cors());

// Used to store the latest sensor data
let sensorData = {};

// ESP32 uploads data (POST /upload)
app.post('/upload', (req, res) => {
  console.log("Receive data:", req.body);
  if (!req.body || typeof req.body !== 'object') {
    return res.status(400).json({ error: "Data format error: must be  JSON" });
  }
  // Add timestamp to the data
  sensorData = { ...req.body, timestamp: new Date().toISOString() };
  console.log(" latest data:", sensorData);
  res.json({ message: "Data received successfully" });
});

// The front end gets the latest data (GET /data)
app.get('/data', (req, res) => {
  res.json(sensorData);
});

// Listen to port 3000 and bind to 0.0.0.0 (allow LAN access)
const PORT = 3000;
app.listen(PORT, '0.0.0.0', () => {
  console.log(` The server is running at http://0.0.0.0:${PORT}`);
  console.log(`You can access it in the same LAN using "http://<your computer IP>:${PORT}"`);
});
											AirQualityDashboard.vue
<template>
    <div class="dashboard-container">

      <!-- Left: Line chart -->
      <div class="chart-section">
        <line-chart :chart-data="chartData" :options="chartOptions"/>
      </div>
      <!-- Right: WHO reference data sheet -->
      <div class="who-section">
        <h3>WHO Reference Data</h3>
        <table>
          <thead>
            <tr>
              <th>Parameter</th>
              <th>WHO Standard</th>
            </tr>
          </thead>
          <tbody>
            <tr v-for="(item, idx) in whoData" :key="idx">
              <td>{{ item.param }}</td>
              <td>{{ item.standard }}</td>
            </tr>
          </tbody>
        </table>
      </div>
    </div>
  </template>
  
  <script>
  import axios from 'axios'
  import LineChart from './lineChart.vue'
  export default {
    name: 'AirQualityDashboard',
    components: {
      'line-chart': LineChart
    },
    data() {
      return {
        // Line chart data
        chartData: {
          labels: [],
          datasets: [
            {
              label: 'PM2.5 (µg/m³)',
              borderColor: 'rgba(54, 162, 235, 1)',
              backgroundColor: 'rgba(54, 162, 235, 0.2)',
              fill: false,
              data: []
            },
            {
              label: 'PM10 (µg/m³)',
              borderColor: 'rgba(255, 99, 132, 1)',
              backgroundColor: 'rgba(255, 99, 132, 0.2)',
              fill: false,
              data: []
            }
          ]
        },
        chartOptions: {
          responsive: true,
          maintainAspectRatio: false,
          scales: {
            x: {
              title: { display: true, text: 'Time' }
            },
            y: {
              title: { display: true, text: 'Concentration' }
            }
          }
        },
       // WHO reference data (table on the right)
        whoData: [
          { param: 'CO',    standard: '15 min: 100 mg/m³, 1h: 35 mg/m³' },
          { param: 'PM2.5', standard: '24h: 25 µg/m³, Annual: 10 µg/m³' },
          { param: 'PM10',  standard: '24h: 50 µg/m³, Annual: 20 µg/m³' },
          { param: 'NO2',   standard: '1h: 200 µg/m³, Annual: 40 µg/m³' },
          { param: 'NH3',   standard: 'No clear WHO standard' },
          { param: 'CH2O',  standard: '30 min: 0.1 mg/m³ (Formaldehyde)' },
          { param: 'CO2',   standard: 'Recommended < 1000 ppm' }
        ]
      }
    },
    methods: {
      // Pull sensor data from the backend
      fetchSensorData() {
        axios.get('http://127.0.0.1:3000/data')
          .then(res => {
            const data = res.data
            // Timeline label
            const label = data.timestamp ? new Date(data.timestamp).toLocaleTimeString() : new Date().toLocaleTimeString()
            // Push new PM2.5, PM10 to chartData
            this.chartData.labels.push(label)
            this.chartData.datasets[0].data.push(data.pm25) // PM2.5
            this.chartData.datasets[1].data.push(data.pm10) // PM10
            // Keep the last 10 points
            if (this.chartData.labels.length > 10) {
              this.chartData.labels.shift()
              this.chartData.datasets.forEach(ds => ds.data.shift())
            }
          })
          .catch(err => {
            console.error('Error fetching sensor data:', err)
          })
      },
      startTimer() {
        // Get data every 5 seconds
        setInterval(() => {
          this.fetchSensorData()
        }, 5000)
      }
    },
    mounted() {
     // Initialize and get it once
      this.fetchSensorData()
      // Start timed refresh
      this.startTimer()
    }
  }
  </script>
  
  <style scoped>
  .dashboard-container {
    display: flex;
    gap: 20px;
    height: 500px;
  }
  .chart-section {
    flex: 1;
    border: 1px solid #ccc;
    position: relative;
  }
  .who-section {
    width: 300px;
    border: 1px solid #ccc;
    padding: 10px;
  }
  .who-section table {
    width: 100%;
    border-collapse: collapse;
  }
  .who-section th, .who-section td {
    border: 1px solid #aaa;
    padding: 6px;
    text-align: left;
  }
  .buttontest {
  font-size: 20px;
  padding: 10px 20px;
  margin-top: 20px;
  cursor: pointer;
}
  </style>
  
  											LineChart.vue
<template>
  <div ref="chartRef" style="width: 100%; height: 400px;"></div>
</template>

<script>
import { markRaw } from 'vue';
import * as echarts from 'echarts';

export default {
  props: {
    chartData: {
      type: Object,
      required: true
    },
    options: {
      type: Object,
      required: true
    },
    test: {
      type: String
    }
  },
  data() {
    return {
      myChart: null
    };
  },
  mounted() {
    this.initChart();
  },
  watch: {
    chartData: {
      handler() {
        this.updateChart();
      },
      deep: true
    },
    options: {
      handler() {
        this.updateChart();
      },
      deep: true
    }
  },
  methods: {
    initChart() {
      const chartRef = this.$refs.chartRef;
      this.myChart = markRaw(echarts.init(chartRef));
      const option = this.generateChartOption();
      this.myChart.setOption(option);
    },
    updateChart() {
      if (this.myChart) {
        const option = this.generateChartOption();
        this.myChart.setOption(option, true);
      }
    },
    generateChartOption() {
      const thresholds = {
        'PM2.5 (µg/m³)': 15,
        'PM10 (µg/m³)': 25,
        'CO (ppm)': 10,
        'NO2 (ppb)': 30,
        'NH3 (ppb)': 1000,
        'MQ2 (ppm)': 300,
        'CO2 (ppm)': 1000
      };

      const markLines = this.chartData.datasets.map((dataset) => {
        const threshold = thresholds[dataset.label];
        return {
          name: `${dataset.label} Threshold`,
          data: [
            {
              yAxis: threshold
            }
          ],
          lineStyle: {
            color: dataset.borderColor,
            type: 'dashed'
          }
        };
      });

      return {
        tooltip: {
          trigger: 'axis'
        },
        legend: {
          data: this.chartData.datasets.map(dataset => dataset.label)
        },
        xAxis: {
          type: 'category',
          data: this.chartData.labels,
          name: this.options.scales.x.title.text
        },
        yAxis: {
          type: 'value',
          name: this.options.scales.y.title.text
        },
        series: this.chartData.datasets.map(dataset => ({
          name: dataset.label,
          type: 'bar',
          data: dataset.data,
          lineStyle: {
            color: dataset.borderColor
          },
          itemStyle: {
            color: dataset.borderColor
          },
          areaStyle: {
            color: dataset.backgroundColor
          },
          fill: dataset.fill,
          markLine: {
            silent: true,
            data: markLines.find(ml => ml.name.includes(dataset.label)).data,
            lineStyle: markLines.find(ml => ml.name.includes(dataset.label)).lineStyle
          }
        }))
      };
    }
  }
};
</script>    
											SensorData.vue
<template>
  <div class="sensor-data">
    <h2>Sensor data</h2>
    <p>MQ-2: {{ sensorData.mq2 }} ppm</p>
    <p>CO: {{ sensorData.co }} ppm</p>
    <p>NO2: {{ sensorData.no2 }} ppb</p>
    <p>NH3: {{ sensorData.nh3 }} ppb</p>
    <p>CO2: {{ sensorData.co2 }} ppm</p>
    <p>PM2.5: {{ sensorData.pm25 }} µg/m³</p>
    <p>PM10: {{ sensorData.pm10 }} µg/m³</p>
    <p v-if="sensorData.timestamp">
      time: {{ new Date(sensorData.timestamp).toLocaleString() }}
    </p>
    <button @click="fetchData">Refresh Data</button>
  </div>
</template>

<script>
import axios from 'axios'

export default {
  name: 'SensorData',
  data() {
    return {
      sensorData: {
        mq2: 0,
        co: 0,
        no2: 0,
        nh3: 0,
        co2: 0,
        pm25: 0,
        pm10: 0,
        timestamp: ''
      }
    }
  },
  methods: {
    fetchData() {
      axios.get('http://192.168.137.1:3000/data')
        .then(response => {
          this.sensorData = response.data
        })
        .catch(err => {
          console.error('Failed to obtain data:', err)
        })
    }
  },
  mounted() {
    // Get it once after the component is loaded
    this.fetchData()
    // Automatically refresh every 10 seconds
    setInterval(this.fetchData, 10000)
  }
}
</script>

<style scoped>
.sensor-data {
  text-align: center;
  font-size: 24px;
  line-height: 1.6;
  max-width: 600px;
  margin: 0 auto;
  padding: 20px;
}

.sensor-data h2 {
  font-size: 28px;
  margin-bottom: 20px;
}

.sensor-data p {
  margin: 8px 0;
}

.sensor-data button {
  font-size: 20px;
  padding: 10px 20px;
  margin-top: 20px;
  cursor: pointer;
}
</style>

												APP.vue
<template>
  <div id="app">
    <AirQualityDashboard />
  </div>
</template>

<script>
import AirQualityDashboard from './components/AirQualityDashboard.vue'

export default {
  name: 'App',
  components: {
    AirQualityDashboard
  }
}
</script>

<style>
#app {
  font-family: Avenir, Helvetica, Arial, sans-serif;
  text-align: center;
  color: #2c3e50;
  margin-top: 0;
}
nav a {
  margin: 0 10px;
  font-weight: bold;
  color: #2c3e50;
}
nav a.router-link-exact-active {
  color: #42b983;
}
</style>
